plugins {
    id 'fml-loom' version '0.1'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = "[你是凋零]" + project.archives_base_name
}

repositories {
    maven {
        name = "ModdedMITE"
        url = uri("https://maven.limingzxc.top/repository/maven-public/")
    }
}

configurations {
    fml
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    fml "com.github.MinecraftIsTooEasy:FishModLoader:3.4.2"
    implementation "com.github.MinecraftIsTooEasy:FishModLoader:3.4.2"

    implementation "com.github.MinecraftIsTooEasy:ManyLib:2.3.0"
    implementation "com.github.MinecraftIsTooEasy:ModMenu:10.1.1"
    implementation "com.github.MinecraftIsTooEasy:RustedIronCore:1.5.0"
}

def fmlResolvedFile = configurations.fml.files.iterator().next()

loom {
    accessWidenerPath = file("src/main/resources/wither.accessWidener")
    mergedMinecraftJar()

    mods {
        "YouAreTheWither" {
            sourceSet sourceSets.main
        }
    }
}

loom.setFML(fmlResolvedFile)
dependencies.add("mappings", loom.fmlMCPMappings())

processResources {
    inputs.property "version", project.version

    filesMatching("fml.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    inputs.property "archivesName", project.base.archivesName

    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}"}
    }
}

tasks.named('runClient', JavaExec).configure { exec ->
    def user = project.hasProperty('username') ? project.username : 'Dev'
    exec.setArgs(['--username', user, '--session', '0', '--width', '854', '--height', '480'])
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}